
use valk:mem

extend Connection {
    bind_data: ?*[<MySQLBind>] (null)
    bind_length: uint (0)
    bind_size: uint (0)
    //
    bind_values: Array[Col] (.{})
    bind_used_values: Array[Col] (.{})

    + fn clear_binds() {
        each this.bind_used_values as b {
            b.clear()
            this.bind_values.append(b)
        }
        this.bind_length = 0
    }
    fn close_binds() {
        if this.bind_size > 0 : mem:free(this.bind_data)
    }

    + fn bind(name: String, value: $T) {
        let col = this.bind_gen(value)
        col.name = name
    }
    + fn bindv(value: $T) {
        this.bind_gen(value)
    }
    - fn bind_gen(value: $T) Col {

        #if is_nullable_type(T)
        if !isset(value) {
            return this.bind_null()
        }
        #end

        #if is_integer_type(T)
        this.bind_int(value)
        #elif is_float_type(T)
        this.bind_double(value)
        #elif is_bool_type(T)
        this.bind_bool(value)
        #elif is_type_of_class(T, String)
        this.bind_string(value)
        #elif is_structural_type(T)
        #loop object value as prop
        this.bind(prop.value)
        #end
        #else
        #print_type(T)
        #error "Cannot bind this value type to the query"
        #end
    }

    + fn bind_null() Col {
        let col = this.get_bind()
        col.type == COLTYPE.null
        return col
    }
    + fn bind_int(value: int) Col {
        let col = this.get_bind()
        col.type == COLTYPE.int
        col.value_int = value
        return col
    }
    + fn bind_float(value: f64) Col {
        let col = this.get_bind()
        col.type == COLTYPE.float
        col.value_float = value
        return col
    }
    + fn bind_bool(value: bool) Col {
        let col = this.get_bind()
        col.type == COLTYPE.bool
        col.value_bool = value
        return col
    }
    + fn bind_string(value: String) Col {
        let col = this.get_bind()
        col.type == COLTYPE.string
        col.value_string = value
        return col
    }

    fn get_bind() Col {
        let col = this.bind_values.pop_last() !? Col{}
        this.bind_used_values.append(col)
        return col
    }

    fn get_mysql_bind() MySQLBind {
        let len = ++this.bind_length
        if len > this.bind_size {
            let old = this.bind_data
            let old_size = size_of(<MySQLBind>) * this.bind_size
            this.bind_size += 10
            let new_size = size_of(<MySQLBind>) * this.bind_size
            this.bind_data = mem:alloc(new_size).@cast(*[<MySQLBind>])
            if isset(old) {
                mem:copy(old, this.bind_data, old_size)
                mem:free(old)
            }
        }
        return @ref(this.bind_data[len - 1])
    }
}
